buildscript{
    repositories {
        maven {
            url pluginrepo_url
            credentials {
                username repo_uid
                password repo_pwd
            }
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE")
        classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.8.1'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.7"
    id "org.sonarqube" version "2.6.2"
    id "com.jfrog.artifactory" version "4.7.4"
}

apply plugin: "java"
apply plugin: 'org.springframework.boot'
apply plugin: 'project-report'

asciidoctorj {
    noDefaultRepositories = true
}

Properties metaProperties = new Properties()
File propertiesFile = new File('appmeta.properties')
propertiesFile.withInputStream {
    metaProperties.load(it)
}

asciidoctor {
    backends 'html5', 'pdf'
    attributes \
        'pdf-stylesdir': "${sourceDir}/theme",
            'pdf-style': 'jack'
}
wrapper {
    gradleVersion = '4.8.1'
}

def rootGroup = metaProperties.root_group
def rootVersion = metaProperties.root_version
def buildId = System.getenv('BUILD_ID')

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = "UTF-8"

test {
    useJUnitPlatform()
    ignoreFailures = true
}

repositories {
    maven {
        url repo_url
        credentials {
            username repo_uid
            password repo_pwd
        }
    }
}

group = rootGroup
version = rootVersion + "." + (buildId ? buildId : "MANUAL-BUILD")
def branch = ( System.getenv('BRANCH_NAME') ? System.getenv("BRANCH_NAME") : 'DESKTOP-BUILD')
def pull_request_no = System.getenv('CHANGE_ID')

sonarqube {
    properties {
        if(branch == 'master' || pull_request_no) {
            property "sonar.projectKey", group + ":" + project.name
            property "sonar.projectName", project.name
            property "sonar.projectVersion", version
            if (pull_request_no) {
                property "sonar.analysis.mode", "preview"
                property "sonar.github.oauth", github_oauth
                property "sonar.github.repository", "hc-sc/radar-" + project.name
                property "sonar.github.pullRequest", pull_request_no
            }
        }
    }
}

tasks.withType(Jar) {
    manifest {
        attributes.putAt("hc-name", project.group + ":" + project.name)
        attributes.putAt("hc-version", version)
        attributes.putAt("hc-branch", System.getenv("GIT_BRANCH") ?: "DESKTOP-BUILD")
        attributes.putAt("hc-commit", System.getenv("GIT_COMMIT") ?: "DESKTOP-BUILD")
        attributes.putAt("hc-build", System.getenv("BUILD_NUMBER") ?: "DESKTOP-BUILD")
        attributes.putAt("hc-date", new Date().format("yy/MM/dd kk:mm"))
    }
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-web:2.0.3.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-amqp:2.0.3.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-jersey:2.0.3.RELEASE')
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.2.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}
